//Teleport to TNO if in party during TeleportParty 
IF 
	ReceivedOrder(Myself,66) 
	Global("qd_ignus_party","GLOBAL",1)
THEN
	RESPONSE #100 
		MoveGlobalObject("Ignus",Protagonist)
		Continue()
END 

//Play Dead if reduced to 1 HP and currently following TNO 
IF 
	HP(Myself,1)
	Global("qd_ignus_play_dead","LOCALS",0)
	Global("qd_ignus_party","GLOBAL",1)
	Allegiance(Myself,FAMILIAR)
THEN
	RESPONSE #100
		SetGlobal("qd_ignus_play_dead","LOCALS",1)
		PlaySound("IGN066")
		SetSequence(SEQ_DIE)
		ApplySpellRES("QDUNDI1",Myself)
		ChangeEnemyAlly(Myself, NEUTRAL)
		Continue() 
END 

//Wake up from Playing Dead if No Enemies in Sight 
IF 
	!ActuallyInCombat()
	Global("qd_ignus_play_dead","LOCALS",1)
	Global("qd_ignus_party","GLOBAL",1)
	
THEN
	RESPONSE #100
		Wait(5)
		SetGlobal("qd_ignus_play_dead","LOCALS",0)
		SetSequence(ANIM_GETUP)
		ApplySpellRES("QDUNDI2",Myself)
		Wait(1)
		ChangeEnemyAlly(Myself, FAMILIAR)
		Continue() 
END 
