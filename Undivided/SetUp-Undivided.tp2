BACKUP ~Undivided/backup~
AUTHOR ~aquadrizzt@gmail.com~
VERSION ~0.0a~ 

/* Language Settings */
AUTO_TRA ~Undivided/tra/%s~
LANGUAGE ~English~ ~english~ ~Undivided/tra/english/setup.tra~

// ========================================================
// VHAILOR
// -----------------------------
// Contents:
// - Make Vhailor follow the party but not use the party interface. 
// - Vhailor automatically levels up with TNO. 
// - Vhailor interjects when possible.
// -----------------------------
// Future Updates: 
// - Allowing Vhailor to comment on party member deaths.
// - Allowing Vhailor to banter with party members. 
// ========================================================

BEGIN @100 

COPY_EXISTING ~aclue.itm~ ~override/qd_und_vhailor.qd~ //check file for this component 

COPY ~Undivided/data/qd_und1.itm~ ~override~ //min HP = 1; fog of war on 
	 // ~Undivided/data/qd_und2.itm~ ~override~ //fog of war on (only equip while in party)
     ~Undivided/data/qdundm1.spl~ ~override/qdundv1.spl~ //vhailor: prot from enemies and disable
	 ~Undivided/data/qdundm2.spl~ ~override/qdundv2.spl~ //vhailor: +1 hp and remove v1
	 ~Undivided/data/qdundm3.spl~ ~override/qdundv3.spl~ //vhailor: levelup heal
		
//slight change to UI to enable vhailor to attack
COPY_EXISTING ~ui.menu~ ~override~ 
	REPLACE_TEXTUALLY ~enabled "buttonArray:GetButtonEnabled(0)"~ ~~
BUT_ONLY

//adding min HP and exploration to Vhailor 
/*
COPY_EXISTING ~vhail.cre~ ~override~ 
	ADD_CRE_ITEM ~qd_und1~ #0 #0 #0 ~NONE~ ~RRING~ EQUIP

COPY_EXISTING ~vhaileg.cre~ ~override~ 
	ADD_CRE_ITEM ~qd_und1~ #0 #0 #0 ~NONE~ ~RRING~ EQUIP
*/ 	
EXTEND_BOTTOM ~pcvhail.bcs~ ~Undivided/data/qd_und_vhailor.baf~ 
	
//Updating script checks for Vhailor 
COPY_EXISTING_REGEXP ~.*\.bcs~ ~override~ 
	DECOMPILE_AND_PATCH BEGIN 
		REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~ActionOverride("Vhail",JoinPartyEx(TRUE))~ ~ActionOverride("Vhail",AddFamiliar()) ChangeEnemyAlly("Vhail",FAMILIAR) AddFamiliar() ApplySpellRES("QDUNDV2",Myself) GiveItemCreate("qd_und1",Myself,1,0,0) XEquipItem("qd_und1","vhail",SLOT_RING_LEFT,1) SetGlobal("Vhailor_Join","GLOBAL",1) SetGlobal("qd_vhailor_party","GLOBAL",1) ActionOverride(Protagonist,DestroyItem("qd_und1"))~ 
		// REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~TeleportParty("\(AR[0-9]*\)",\[\([0-9]*\)\.\([0-9]*\)\],\([0-9]*\))~ ~ TeleportParty("\1",[\2.\3],\4) GiveOrder(Protagonist,66)~ 
		REFACTOR_TRIGGER CASE_INSENSITIVE EXACT_MATCH ~InParty("Vhail")~ ~Global("qd_vhailor_party","GLOBAL",1)~ 
	END 
BUT_ONLY 

//Vhailor will use Party AI as appropriate
COPY_EXISTING ~pcvhail.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN 
		REFACTOR_TRIGGER CASE_INSENSITIVE EXACT_MATCH ~InParty(Myself)~ ~Global("qd_vhailor_party","GLOBAL",1)~ 
	END 
BUT_ONLY 

//Delete original Vhailor spawn from Curst Prison 
COPY_EXISTING ~ar0702.are~ ~override~
	LPF fj_are_structure
		INT_VAR 
			fj_delete_mode 	  = 7
		STR_VAR
			fj_structure_type = actor
	END
	
//add Vhailor to somewhere hidden in starting area 
COPY_EXISTING ~ar0202.are~ ~override~
	LPF fj_are_structure
		INT_VAR
			fj_loc_x          = 2180
			fj_loc_y          = 2531
		STR_VAR
			fj_structure_type = actor
			fj_cre_resref     = vhail
	END
	
//makes vhailor a global creature and then teleports him to his actual spawn location
<<<<<<<< QD/vhailor_move.baf
IF
	!Global("qd_vhailor_global","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("qd_vhailor_global","GLOBAL",1)
		ActionOverride("Vhail",MakeGlobalOverride())
		Continue()
END

IF 
	!Global("qd_vhailor_moved","GLOBAL",1) 
	Global("qd_vhailor_global","GLOBAL",1) 
THEN 
	RESPONSE #100 
		SetGlobal("qd_vhailor_moved","GLOBAL",1) 
		MoveGlobal("AR0702","Vhail",[4647.454])
		Continue()
END 

// this script should shift Vhailor to the location of the protagonist outside of player-exclusive zones
IF 
	!AreaCheckObject("AR1200",Protagonist)
	!AreaCheckObject("AR1201",Protagonist)
	!AreaCheckObject("AR1202",Protagonist)
	!AreaCheckObject("AR1203",Protagonist)
	!AreaCheckObject("AR1800",Protagonist)
	!AreaCheckObject("AR1900",Protagonist)
	!InActiveArea("Vhail")
	Global("qd_vhailor_party","GLOBAL",1) 
	Global("qd_vhailor_global","GLOBAL",1)
	Global("qd_vhailor_maze","GLOBAL",0)
THEN 
	RESPONSE #100 
		MoveGlobalObject("Vhail",Protagonist)
		Continue()
END 
>>>>>>>> 

EXTEND_BOTTOM ~baldur.bcs~ ~QD/vhailor_move.baf~ 

//generating xp progression script for morte 	
<<<<<<<< QD/vhailor_leveling.baf
	IF
		Global("qd_vhailor_party","GLOBAL",1)
		XPGT(Protagonist,%xp%)
		XPLT(Protagonist,%xp_next%)
		LevelLT(Myself,%lvl%)
	THEN	
		RESPONSE #100 
			SetGlobal("qd_vhailor_level","GLOBAL",%lvl%)
			ChangeStat(Myself,LEVEL,%lvl%,SET)
			ChangeStat(Myself,MAXHITPOINTS,%hp%,SET)
			ChangeStat(Myself,SAVEVSDEATH,%sv_d%,SET)
			ChangeStat(Myself,SAVEVSWANDS,%sv_w%,SET)
			ChangeStat(Myself,SAVEVSPOLY,%sv_p%,SET)
			ChangeStat(Myself,SAVEVSBREATH,%sv_b%,SET)
			ChangeStat(Myself,SAVEVSSPELL,%sv_s%,SET)
			PlaySound("VHA111")
			ApplySpellRES("QDUNDV3",Myself)
			Continue() 
	END 
>>>>>>>>

<<<<<<<< QD/vhailor_leveling_max.baf
	IF
		Global("qd_vhailor_party","GLOBAL",1)
		XPGT(Protagonist,%xp%)
		LevelLT(Myself,%lvl%)
	THEN	
		RESPONSE #100 
			SetGlobal("qd_vhailor_level","GLOBAL",%lvl%)
			ChangeStat(Myself,LEVEL,%lvl%,SET)
			ChangeStat(Myself,MAXHITPOINTS,%hp%,SET)
			ChangeStat(Myself,SAVEVSDEATH,%sv_d%,SET)
			ChangeStat(Myself,SAVEVSWANDS,%sv_w%,SET)
			ChangeStat(Myself,SAVEVSPOLY,%sv_p%,SET)
			ChangeStat(Myself,SAVEVSBREATH,%sv_b%,SET)
			ChangeStat(Myself,SAVEVSSPELL,%sv_s%,SET)
			PlaySound("VHA111")
			ApplySpellRES("QDUNDV3",Myself)
			Continue() 
	END 
>>>>>>>>

COPY_EXISTING ~XPLEVEL.2DA~ ~override~
	COUNT_2DA_COLS num_cols
	COUNT_2DA_ROWS num_cols num_rows
	READ_2DA_ENTRIES_NOW ~r2en_xp_level~ num_cols
	FOR (col = 1; col < num_cols; col += 1) BEGIN
		READ_2DA_ENTRY_FORMER ~r2en_xp_level~ 2 col xp
		PATCH_IF (col < 20) THEN BEGIN 
			READ_2DA_ENTRY_FORMER ~r2en_xp_level~ 2 col+1 xp_next
		END 
		INNER_ACTION BEGIN 
			COPY_EXISTING ~savewar.2da~ ~override~ 
				COUNT_2DA_COLS save_cols
				READ_2DA_ENTRY 0 col save_cols death
				READ_2DA_ENTRY 1 col save_cols wands
				READ_2DA_ENTRY 2 col save_cols poly
				READ_2DA_ENTRY 3 col save_cols breath
				READ_2DA_ENTRY 4 col save_cols spell
			ACTION_IF (col < 20) THEN BEGIN 
				ACTION_IF (col < 10) THEN BEGIN 
					EXTEND_BOTTOM ~pcvhail.bcs~ ~QD/vhailor_leveling.baf~ 
						prev = (%col%)-1
						lvl = (%col%)
						xp = (%xp%)
						xp_next = (%xp_next%)
						hp = (12*%col%)
						sv_d = %death%
						sv_w = %wands%
						sv_p = %poly%
						sv_b = %breath%
						sv_s = %spell%
					EVAL 
				END 
				ELSE BEGIN 
					EXTEND_BOTTOM ~pcvhail.bcs~ ~QD/vhailor_leveling.baf~ 
						lvl = (%col%)
						xp = (%xp%)
						xp_next = (%xp_next%)
						hp = (108 + 3*(%col%-9))
						sv_d = %death%
						sv_w = %wands%
						sv_p = %poly%
						sv_b = %breath%
						sv_s = %spell%
					EVAL 
				END 
			END 
			ELSE BEGIN 
				EXTEND_BOTTOM ~pcvhail.bcs~ ~QD/vhailor_leveling_max.baf~ 
					prev = (%col%)-1
					lvl = (%col%)
					xp = (%xp%)
					hp = (108 + 3*(%col%-9))
					sv_d = %death%
					sv_w = %wands%
					sv_p = %poly%
					sv_b = %breath%
					sv_s = %spell%
				EVAL 
			END 
		END 
	END 

// Minor dialog patches to Vhailor
COMPILE ~Undivided/data/qd_und_vhailor_dlg.d~

//Tomb of the Nameless One removal and Drowned Nations rejoin
<<<<<<<< QD/1800_tomb_vhailor.baf
IF
	Global("qd_vhailor_party","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("qd_vhailor_party","GLOBAL",0)
		SetGlobal("qd_vhailor_tomb","GLOBAL",1)
		ActionOverride("Vhail",RemoveFamiliar())
		ChangeEnemyAlly("Vhail",NEUTRAL)  
		Continue()
END

IF
	Global("qd_vhailor_tomb","GLOBAL",1)
THEN
	RESPONSE #100 
		SetGlobal("qd_vhailor_tomb","GLOBAL",0)
		ActionOverride("Vhail",MoveGlobal("AR1700",Myself,[4000.2600]))
		Continue()
END 
>>>>>>>> 

EXTEND_BOTTOM ~ar1800.bcs~ ~QD/1800_tomb_vhailor.baf~ 

COPY_EXISTING ~ar1700.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN 
		REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~ActionOverride("Vhail",JoinPartyEx(TRUE))~ ~SetGlobal("qd_vhailor_party","GLOBAL",1) ActionOverride("Vhail",AddFamiliar()) 		ChangeEnemyAlly("Vhail",FAMILIAR)~ 
	END 
BUT_ONLY 

//Player's Maze and teleport rejoin 
<<<<<<<< QD/1900_maze_vhailor.baf
IF
	Global("qd_vhailor_party","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("qd_vhailor_party","GLOBAL",0)
		SetGlobal("qd_vhailor_maze","GLOBAL",1)
		ActionOverride("Vhail",RemoveFamiliar())
		ChangeEnemyAlly("Vhail",NEUTRAL)  
		Continue()
END

IF
	Global("qd_vhailor_maze","GLOBAL",1)
THEN
	RESPONSE #100 
		SetGlobal("qd_vhailor_maze","GLOBAL",0)
		ActionOverride("Vhail",MoveGlobal("AR1700",Myself,[4000.2600]))
		Continue()
END 
>>>>>>>> 

EXTEND_TOP ~1900name.bcs~ ~QD/1900_maze_vhailor.baf~ 

//Fortress of Regrets removal
<<<<<<<< QD/1200_fortress_vhailor.baf
IF
	Global("qd_vhailor_party","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("qd_vhailor_party","GLOBAL",0)
		SetGlobal("qd_vhailor_fortress","GLOBAL",1)
		ActionOverride("Vhail",RemoveFamiliar())
		ChangeEnemyAlly("Vhail",NEUTRAL)  
		Continue()
END

IF
	Global("qd_vhailor_fortress","GLOBAL",1)
THEN
	RESPONSE #100 
		SetGlobal("qd_vhailor_fortress","GLOBAL",0)
		ActionOverride("Vhail",MoveGlobal("AR0202",Myself,[4000.2600]))
		Continue()
END 
>>>>>>>> 

EXTEND_TOP ~ar1200.bcs~ ~QD/1200_fortress_vhailor.baf~ 

