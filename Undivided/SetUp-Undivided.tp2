BACKUP ~Undivided/backup~
AUTHOR ~aquadrizzt@gmail.com~
VERSION ~0.0~ 

/* Language Settings */
AUTO_TRA ~Undivided/tra/%s~
LANGUAGE ~English~ ~english~ ~Undivided/tra/english/setup.tra~

// ========================================================
// MAIN COMPONENT
// -----------------------------
// Contents:
// - Make Nordom and Morte follow the party but not use the party interface. 
// ========================================================

BEGIN @0

// Minor dialog patches to Nordom and Morte
COMPILE ~Undivided/data/qd_und_patch.d~

COPY ~Undivided/data/qd_und1.itm~ ~override~ //min HP = 1; clear fog of war
COPY ~Undivided/data/qdund1.spl~ ~override~ //heals morte for 1

//adding min HP and exploration to Morte 
COPY_EXISTING ~morte.cre~ ~override~ 
	ADD_CRE_ITEM ~qd_und1~ #0 #0 #0 ~NONE~ ~LRING RRING~ EQUIP

COPY_EXISTING ~morteeg.cre~ ~override~ 
	ADD_CRE_ITEM ~qd_und1~ #0 #0 #0 ~NONE~ ~LRING RRING~ EQUIP
	
COPY_EXISTING ~mortef.cre~ ~override~ 
	ADD_CRE_ITEM ~qd_und1~ #0 #0 #0 ~NONE~ ~LRING RRING~ EQUIP 
	
EXTEND_BOTTOM ~pcmorte.bcs~ ~Undivided/data/qd_und_morte.baf~ 

//slight change to UI to enable morte and nordom to use their weapons
COPY_EXISTING ~ui.menu~ ~override~ 
	REPLACE_TEXTUALLY ~enabled "buttonArray:GetButtonEnabled(0)"~ ~~
	
//Updating script checks for morte 
COPY_EXISTING_REGEXP ~.*\.bcs~ ~override~ 
	DECOMPILE_AND_PATCH BEGIN 
		REFACTOR_TRIGGER CASE_INSENSITIVE EXACT_MATCH ~InParty("Morte")~ ~Global("qd_morte_party","GLOBAL",1)~ 
	END 
BUT_ONLY 

//Updates Fell's tattoos with new party checks. 
COPY_EXISTING ~fell.sto~ ~override~
	GET_OFFSET_ARRAY tattoos 0x34 4 0x38 4 0 0 0x58 
	PHP_EACH tattoos AS int => tt_off BEGIN 
		READ_ASCII tt_off itmres
		PATCH_IF (~%itmres%~ STRING_EQUAL_CASE ~ttmorte1~) BEGIN 
			WRITE_LONG (tt_off + 0x1c) RESOLVE_STR_REF(@10) 
		END 
		PATCH_IF (~%itmres%~ STRING_EQUAL_CASE ~ttcubed~) BEGIN 
			WRITE_LONG (tt_off + 0x1c) RESOLVE_STR_REF(@11)
		END 
	END 